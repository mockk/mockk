ext {
    mavenName = 'MockK Android'
    mavenDescription = 'mocking library for Kotlin (Android instrumented test)'
}
apply plugin: 'com.android.library'
apply from: "${gradles}/android-module.gradle"
apply from: "${gradles}/upload.gradle"

android {
    compileSdkVersion 'android-28'
    buildToolsVersion android_build_tools_version

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
        warning 'NewApi'
    }

    packagingOptions {
        exclude 'META-INF/main.kotlin_module'
        exclude 'META-INF/LICENSE.md'
        exclude 'META-INF/LICENSE-notice.md'
    }

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        versionName project['version']
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArgument "notAnnotation", "io.mockk.test.SkipInstrumentedAndroidTest"
    }

    sourceSets {
        androidTest.kotlin.srcDirs += '../common/src/test/kotlin'
    }

    compileOptions {
        sourceCompatibility = 1.8
        targetCompatibility = 1.8
    }

}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // very weird hack to make it working in IDE (check settings.gradle)
    def mockKProject = findProject(':mockk-jvm') ?: project(":mockk")
    api(mockKProject, {
        exclude group: 'io.mockk', module: 'mockk-agent-jvm'
    })
    implementation project(":mockk-agent-android")
    implementation project(":mockk-agent-api")

    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    androidTestImplementation("org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version") {
        exclude group: "junit", module: "junit"
    }

    androidTestImplementation "org.junit.jupiter:junit-jupiter-api:$junit_jupiter_version"
    androidTestImplementation "org.junit.jupiter:junit-jupiter-engine:$junit_jupiter_version"
    androidTestImplementation "org.junit.vintage:junit-vintage-engine:$junit_vintage_version"
}

