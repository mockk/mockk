ext {
    mavenName = 'MockK common'
    mavenDescription = 'Common(JS and Java) MockK module'
}
apply from: "${gradles}/common-module.gradle"
apply from: "${gradles}/upload-archives.gradle"
apply from: "${gradles}/additional-archives.gradle"

configurations {
    sampleTestCompile.extendsFrom testCompile
    sampleTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile project(":mockk-dsl")
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

sourceSets {
    sample {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/sample/kotlin')
        }
        resources {
            srcDir file('src/sample/resources')
        }
    }
}

task samples(type: Test) {
    setTestClassesDirs(sourceSets.sample.output.classesDirs)
    classpath = sourceSets.sample.runtimeClasspath
}

jar {
    eachFile { details ->
        if (details.path == "META-INF/main.kotlin_module") {
            details.path = "META-INF/mockk-common.kotlin_module"
        }
    }
}
