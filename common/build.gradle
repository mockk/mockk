apply plugin: 'kotlin-platform-common'

configurations {
    sampleTestCompile.extendsFrom testCompile
    sampleTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile project(":mockk-dsl")
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

sourceSets {
    sample {
        kotlin {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/sample/kotlin')
        }
        resources.srcDir file('src/sample/resources')
    }
}


task samples(type: Test) {
    setTestClassesDirs(sourceSets.sample.output.classesDirs)
    classpath = sourceSets.sample.runtimeClasspath
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

jar {
    eachFile { details ->
        if (details.path == "META-INF/main.kotlin_module") {
            details.path = "META-INF/mockk-common.kotlin_module"
        }
    }
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('localrepo')) {
                repository(url: "file://" + rootProject.properties['localrepo'] ?: '')
            } else {
                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
                }
            }

            pom.project {
                name 'MockK common'
                packaging 'jar'
                description 'Common(JS and Java) MockK module'
                url 'http://mockk.io'

                scm {
                    connection 'scm:git:git@github.com:mockk/mockk.git'
                    developerConnection 'scm:git:git@github.com:mockk/mockk.git'
                    url 'http://www.github.com/mockk/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}

